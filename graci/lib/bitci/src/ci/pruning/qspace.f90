!**********************************************************************
! Calculation of information about the discarded (Q-space)
! configurations
!**********************************************************************
module qspace

  implicit none

contains

!######################################################################
! qspace_e2: computes the contributions to the 2nd-order ENPT2 energy
!            corrections from the Q-space configurations
!######################################################################
  subroutine qspace_e2(cfg,csfdim,nvec,Avec,hdiag,E0,i1I,i2I,i1E,&
       i2E,i1I1E,n1I,n2I,n1E,n2E,n1I1E,E2Q)

    use constants
    use bitglobal
    use conftype
    
    implicit none

    ! MRCI configuration derived type
    type(mrcfg), intent(in)  :: cfg
    
    ! Dimensions
    integer(is), intent(in)  :: csfdim,nvec,n1I,n2I,n1E,n2E,n1I1E

    ! A-vectors
    real(dp), intent(in)     :: Avec(csfdim,nvec)

    ! On-diagonal Hamiltonian matrix elements
    real(dp), intent(in)     :: hdiag(csfdim)

    ! Zeroth-order (ref space) energies
    real(dp), intent(in)     :: E0(nvec)
    real(dp)                 :: eig0(nvec)
    
    ! Surviving configuration flags
    integer(is), intent(in)  :: i1I(n1I),i2I(n2I),i1E(n1E),i2E(n2E),&
                                i1I1E(n1I1E)

    ! Q-space contributions to the 2nd-order energy corrections
    real(dp), intent(out)    :: E2Q(nvec)
    
    ! Everything else
    integer(is)              :: n,ioff,icsf,j
    real(dp)                 :: f

!----------------------------------------------------------------------
! Subtract off E_SCF and E_nuc from the energies to get the true
! reference space eigenvalues
!----------------------------------------------------------------------    
    eig0=E0-escf-enuc
    
!----------------------------------------------------------------------
! Initialisation
!----------------------------------------------------------------------
    E2Q=0.0d0
    
!----------------------------------------------------------------------
! Q-space 1I contributions
!----------------------------------------------------------------------
    if (cfg%n1I > 0) then
       
       ! Loop over 1-hole configurations
       do n=1,cfg%n1h

          ! Loop over the 1I configurations generated by the 1-hole
          ! configuration
          do ioff=cfg%off1I(n),cfg%off1I(n+1)-1

             ! Cycle if we are at a P-space configuration
             if (i1I(ioff) == 1) cycle

             ! Loop over states
             do j=1,nvec
             
                ! Loop over CSFs generated by this configuration
                do icsf=cfg%csfs1I(ioff),cfg%csfs1I(ioff+1)-1

                   ! Contribution to the Q-space energy correction
                   f=Avec(icsf,j)*(eig0(j)-hdiag(icsf))
                   E2Q(j)=E2Q(j)+f**2/(eig0(j)-hdiag(icsf))
                
                enddo

             enddo
             
          enddo
          
       enddo

    endif

!----------------------------------------------------------------------
! Q-space 1E contributions
!----------------------------------------------------------------------
    if (cfg%n1E > 0) then
       
       ! Loop over 1-hole configurations
       do n=1,cfg%n1h

          ! Loop over the 1E configurations generated by the 1-hole
          ! configuration
          do ioff=cfg%off1E(n),cfg%off1E(n+1)-1

             ! Cycle if we are at a P-space configuration
             if (i1E(ioff) == 1) cycle

             ! Loop over states
             do j=1,nvec
             
                ! Loop over CSFs generated by this configuration
                do icsf=cfg%csfs1E(ioff),cfg%csfs1E(ioff+1)-1

                   ! Contribution to the Q-space energy correction
                   f=Avec(icsf,j)*(eig0(j)-hdiag(icsf))
                   E2Q(j)=E2Q(j)+f**2/(eig0(j)-hdiag(icsf))
                
                enddo

             enddo
             
          enddo
          
       enddo

    endif
    
!----------------------------------------------------------------------
! Q-space 2I contributions
!----------------------------------------------------------------------
    if (cfg%n2I > 0) then
       
       ! Loop over 2-hole configurations
       do n=1,cfg%n2h

          ! Loop over the 2I configurations generated by the 2-hole
          ! configuration
          do ioff=cfg%off2I(n),cfg%off2I(n+1)-1

             ! Cycle if we are at a P-space configuration
             if (i2I(ioff) == 1) cycle

             ! Loop over states
             do j=1,nvec
             
                ! Loop over CSFs generated by this configuration
                do icsf=cfg%csfs2I(ioff),cfg%csfs2I(ioff+1)-1

                   ! Contribution to the Q-space energy correction
                   f=Avec(icsf,j)*(eig0(j)-hdiag(icsf))
                   E2Q(j)=E2Q(j)+f**2/(eig0(j)-hdiag(icsf))
                
                enddo

             enddo
             
          enddo
          
       enddo

    endif

!----------------------------------------------------------------------
! Q-space 2E contributions
!----------------------------------------------------------------------
    if (cfg%n2E > 0) then
       
       ! Loop over 2-hole configurations
       do n=1,cfg%n2h

          ! Loop over the 2E configurations generated by the 2-hole
          ! configuration
          do ioff=cfg%off2E(n),cfg%off2E(n+1)-1

             ! Cycle if we are at a P-space configuration
             if (i2E(ioff) == 1) cycle

             ! Loop over states
             do j=1,nvec
             
                ! Loop over CSFs generated by this configuration
                do icsf=cfg%csfs2E(ioff),cfg%csfs2E(ioff+1)-1

                   ! Contribution to the Q-space energy correction
                   f=Avec(icsf,j)*(eig0(j)-hdiag(icsf))
                   E2Q(j)=E2Q(j)+f**2/(eig0(j)-hdiag(icsf))
                
                enddo

             enddo
             
          enddo
          
       enddo

    endif
    

!----------------------------------------------------------------------
! Q-space 1I1E contributions
!----------------------------------------------------------------------
    if (cfg%n1I1E > 0) then
       
       ! Loop over 2-hole configurations
       do n=1,cfg%n2h

          ! Loop over the 1I1E configurations generated by the 2-hole
          ! configuration
          do ioff=cfg%off1I1E(n),cfg%off1I1E(n+1)-1

             ! Cycle if we are at a P-space configuration
             if (i1I1E(ioff) == 1) cycle

             ! Loop over states
             do j=1,nvec
             
                ! Loop over CSFs generated by this configuration
                do icsf=cfg%csfs1I1E(ioff),cfg%csfs1I1E(ioff+1)-1

                   ! Contribution to the Q-space energy correction
                   f=Avec(icsf,j)*(eig0(j)-hdiag(icsf))
                   E2Q(j)=E2Q(j)+f**2/(eig0(j)-hdiag(icsf))
                
                enddo

             enddo
             
          enddo
          
       enddo

    endif
    
    return
    
  end subroutine qspace_e2

!######################################################################
! get_qcorr: Given a set of MRCI eigenvectors, determines the
!            corresponding Q-space energy corrections for a single
!            irrep
!######################################################################
  subroutine get_qcorr(irrep,vecscr,vec0scr,confscr,eqscr,nroots,&
       nextra,qcorr,max_overlap)
    
    use constants
    use bitglobal
    use conftype
    use iomod
    
    implicit none

    ! Irrep number
    integer(is), intent(in)  :: irrep
    
    ! MRCI and ref space eigenvector scratch file numbers
    integer(is), intent(in)  :: vecscr,vec0scr

    ! MRCI configuration scratch file number
    integer(is), intent(in)  :: confscr

    ! Q-space energy correction scratch file number
    integer(is), intent(in)  :: eqscr

    ! No. MRCI roots
    integer(is), intent(in)  :: nroots

    ! No. extra ref space roots
    integer(is), intent(in)  :: nextra

    ! Q-space energy corrections
    real(dp), intent(out)    :: qcorr(nroots)
    real(dp), allocatable    :: qcorr_all(:)

    ! <Ref|MRCI> overlaps
    real(dp), intent(out)    :: max_overlap(nroots)
    
    ! MRCI configuration derived type
    type(mrcfg)              :: cfg

    ! Reference space eigenpairs
    integer(is)              :: csfdim0
    real(dp), allocatable    :: vec0(:,:),E0(:)

    ! MRCI eigenpairs
    integer(is)              :: csfdim
    real(dp), allocatable    :: vec(:,:),E(:)

    ! Ref-to-MRCI state mapping
    integer(is), allocatable :: r2m(:)
    real(dp)                 :: overlap
    
    ! Everything else
    integer(is)              :: nvec,n,i,j
    integer(is)              :: iscratch
    
!----------------------------------------------------------------------
! Set up the MRCI configuration derived type
!----------------------------------------------------------------------
    call cfg%initialise(irrep,confscr)

!----------------------------------------------------------------------
! No. ref space CSFs
!----------------------------------------------------------------------
    csfdim0=0
    do n=1,cfg%n0h
       csfdim0=csfdim0+cfg%csfs0h(n+1)-cfg%csfs0h(n)
    enddo
    
!----------------------------------------------------------------------
! Allocate arrays
!----------------------------------------------------------------------
    ! Total no. ref space roots
    nvec=nroots+nextra

    ! Ref space eigenpairs
    allocate(vec0(csfdim0,nvec))
    allocate(E0(nvec))
    vec0=0.0d0
    E0=0.0d0

    ! MRCI eigenpairs
    csfdim=cfg%csfdim
    allocate(vec(csfdim,nroots))
    allocate(E(nroots))
    vec=0.0d0
    E=0.0d0

    ! Ref-to-MRCI state mapping
    allocate(r2m(nroots))
    r2m=0

    ! Q-space energy corrections (all ref space roots)
    allocate(qcorr_all(nvec))
    qcorr_all=0.0d0
    
!----------------------------------------------------------------------
! Read in all the ref space and MRCI eigenpairs
!----------------------------------------------------------------------
    ! Ref space
    call read_all_eigenpairs(vec0scr,vec0,E0,csfdim0,nvec)

    ! MRCI
    call read_all_eigenpairs(vecscr,vec,E,csfdim,nroots)
    
!----------------------------------------------------------------------
! Determine which Q-space energy corrections to use based on the
! overlaps of the reference and MRCI eigenvectors
!----------------------------------------------------------------------
    ! Initialisation
    r2m=0
    max_overlap=0.0d0

    ! Loop over MRCI roots
    do i=1,nroots

       ! Loop over ref space roots
       do j=1,nvec

          ! Overlap between the MRCI and ref space eigenvectors
          overlap=abs(dot_product(vec(1:csfdim0,i),vec0(:,j)))

          ! Update the maximim overlap and mapping arrays
          if (overlap > max_overlap(i)) then
             max_overlap(i)=overlap
             r2m(i)=j
          endif
          
       enddo
       
    enddo

!----------------------------------------------------------------------
! Read in the Q-space energy corrections for all ref space roots
!----------------------------------------------------------------------
    ! Open the E2Q file
    iscratch=scrunit(eqscr)
    open(iscratch,file=scrname(eqscr),form='unformatted',status='old')

    ! Number vectors
    read(iscratch) n

    ! Consistency check
    if (n /= nvec) then
       errmsg='Error in get_qcorr: inconsistent numbers of ref '&
            //'space vectors'
       call error_control
    endif
    
    ! Q-space energy corrections
    read(iscratch) qcorr_all
    
    ! Close the eigenvector file
    close(iscratch)

!----------------------------------------------------------------------
! Fill in the array of Q-space energy corrections
!----------------------------------------------------------------------
    do i=1,nroots
       qcorr(i)=qcorr_all(r2m(i))
    enddo
    
!----------------------------------------------------------------------
! Deallocate arrays
!----------------------------------------------------------------------
    deallocate(vec0)
    deallocate(E0)
    deallocate(vec)
    deallocate(E)
    deallocate(r2m)
    deallocate(qcorr_all)
    
    return
    
  end subroutine get_qcorr
  
!######################################################################
  
end module qspace
